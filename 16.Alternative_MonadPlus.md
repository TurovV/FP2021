## 16. Классы типов Alternative и MonadPlus и их представители.
# Alternative
* Нужно зашить логическое или на уровень парсера. 
* Расширяем апликативного интерфейса операцией сумма. 
* Содержит бинарную ассоциативную операцию и нейтральный элемент.

Наделяем аппликативный функтор дополнительной моноидальной операцией с семантикой «сложения».

```haskell
class Applicative f => Alternative f where
  empty :: f a
  (<|>) :: f a -> f a -> f a
infixl 3 <|>
```
Почему не можем использовать моноид? \
Моноид -- простой тип, а тут контейрный. Не всегда можем сделать его моноидом так же как делаем с простым типом.
#### для списка
Представитель Alternative для списков полностью повторяет
определение моноида для списка.
```haskell
instance Alternative [] where
  empty :: [a]
  empty = []
  
  (<|>) :: [a] -> [a] -> [a]
  (<|>) = (++)
```
#### для maybe
Представитель Alternative для Maybe ведёт себя, как упаковка
First, возвращая первый не-Nothing в цепочке альтернатив
```haskell
instance Alternative Maybe where
  empty :: Maybe a
  empty = Nothing

  (<|>) :: Maybe a -> Maybe a -> Maybe a
  Nothing <|> m = m
  m <|> _ = m
```
```haskell
GHCi> Nothing <|> Just 3 <|> Just 5 <|> Nothing
Just 3
```

### Интерфейс Alternative для парсера
```haskell
instance Alternative (Parser tok) where
  empty :: Parser tok a
  empty = Parser $ \_ -> Nothing
  
  (<|>) :: Parser tok a -> Parser tok a -> Parser tok a
  Parser u <|> Parser v = Parser f where
  f xs = case u xs of
    Nothing -> v xs
    z -> z
```
Есть альтернатива из двух парсеров, получаем парсер.\
Результирующий парсер на входном потоке токенов работает так:\
* вычисляется первый парсер на входном потоке токенов
* если удачно, игнорируем второй парсер, возвращаем результат
* если недачен, запускаем второй парсер на том же потоке и его результат -- результат всего вычисления

empty — нейтральный элемент, значит парсер, всегда возвращающий неудачу;

#### примеры
```haskell
GHCi> runParser (char 'A' <|> char 'B') "ABC"
Just ("BC",'A')

GHCi> runParser (char 'A' <|> char 'B') "BCD"
Just ("CD",'B')
```

**рекурсивный**\
проходимся по строке: пока все в нижнем регистре, конструируем список символов, как только что-то в верхнем регистре завершаемся и возвращаем результат.\
если lower возвращает Nothing, переходим в pure -- возвращаем пустую строку\
иначе вызываем lowers на строке на единицу короче

```haskell
lowers :: Parser Char String
lowers = (:) <$> lower <*> lowers <|> pure ""
```

```haskell
GHCi> runParser lowers "abCd"
Just ("Cd","ab")

GHCi> runParser lowers "abcd"
Just ("","abcd")

GHCi> runParser lowers "Abcd"
Just ("Abcd","")
```

#### минимальное полное определение
```haskell
infixl 3 <|>

class Applicative f => Alternative f where
  empty :: f a
  (<|>) :: f a -> f a -> f a

  some, many :: f a -> f [a]
  some v = (:) <$> v <*> many v -- One or more

  many v = some v <|> pure [] -- Zero or more

  optional :: Alternative f => f a -> f (Maybe a)
  optional v = Just <$> v <|> pure Nothing
```

можно так:
```haskell
lowers = many lower
```

#### Представитель Alternative для IO

Нужно обрабатывать ошибки.
```haskell
instance Alternative Maybe where
  empty :: IO a
  empty = failIO "mzero"

  (<|>) :: IO a -> IO a -> IO a
  m <|> n = m `catchException` \ (_ :: IOError) -> n
```
Запусти m, если там ошибка, то запусти n

#### примеры
```haskell
GHCi> readFile "exists.txt" <|> return "File doesn't exist."
"Contents.\nAlso contents."

GHCi> readFile "noSuchFile.txt" <|> return "File doesn't exist."
"File doesn't exist."
```
### MonadPlus
Исторически расширял монаду (таким же образом, как alternative расширяет applicative, когда не было alternative и alternative.\
Теперь играет вспомогательную роль (фактически реализация alternative с контекстом monad).

```haskell
class (Alternative m, Monad m) => MonadPlus m where
  mzero :: m a
  mzero = empty

  mplus :: m a -> m a -> m a
  mplus = (<|>)
```	
Методы задают моноид для монад

#### Минимальное полное определение: ничего не делать.
```haskell
instance MonadPlus Maybe

instance MonadPlus IO

instance Alternative [] where
  empty = []
  (<|>) = (++)

instance MonadPlus []
```
#### законы
В MonadPlus есть закон, которых нет в alternative. (потому что взаимодействуют с операцией из Monad).\
Left and Right Zero\
(mzero для любой стрелки Клейсли должен возвращать mzero\
bind с mzero -- mzero)
```haskell
mzero >>= k ≡ mzero
v >> mzero ≡ mzero
```
и по крайней мере одного из двух\
Left Distribution\
(дистрибутивность)
```haskell
(a `mplus` b) >>= k ≡ (a >>= k) `mplus` (b >>= k)
```
Left Catch law\
(безэффектный return, мы прибавляем к нему b, то результат безэффектный).
```haskell
return a `mplus` b ≡ return a
```

### использование MonadPlus
#### guard
Хотим, чтобы какие-то ветки вычисления "засохли", организовать фильтрацию\
если условие выполняется, то вычисления продолжаются в виде одной цепочки, иначе засыхают.\
управляем структурой контейнера, можем построить два контейнера по умолчанию. 
```haskell
-- Haskell 2010 :: MonadPlus m => Bool -> m ()
guard :: Alternative f => Bool -> f ()
guard True = pure ()
guard False = empty
```
```haskell
pythags = do z <- [1..]
x <- [1..z]
y <- [x..z]
guard (x^2 + y^2 == z^2)
return (x, y, z)
```
```haskell
GHCi> take 5 pythags
[(3,4,5),(6,8,10),(5,12,13),(9,12,15),(8,15,17)]
```
#### asum, msum
Суммирование контейнера. префикс m -- monad, a -- alternative\
получаем fold, но еще коллекционируем все эффекты. 
```haskell
asum :: (Foldable t, Alternative f) => t (f a) -> f a
asum = foldr (<|>) empty

msum :: (Foldable t, MonadPlus m) => t (m a) -> m a
msum = asum -- foldr mplus mzero
```
#### mfilter

```haskell
mfilter :: MonadPlus m => (a -> Bool) -> m a -> m a
mfilter p ma = do
  a <- ma
  if p a
  then return a
  else mzero
```



